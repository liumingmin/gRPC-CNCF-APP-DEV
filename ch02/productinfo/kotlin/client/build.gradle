buildscript {
    ext.kotlin_version = '1.3.61'
    ext.coroutines_version = '1.3.3'
    ext.protobuf_version = '3.11.1'
    ext.grpc_version = '1.28.0' // CURRENT_GRPC_VERSION
    ext.grpc_kotlin_version = '0.1.1' // CURRENT_GRPC_KOTLIN_VERSION

    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.google.protobuf:protobuf-gradle-plugin:0.8.11'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'kotlin'
apply plugin: 'com.google.protobuf'
apply plugin: 'idea'
apply plugin: 'application'

repositories {
    google()
    jcenter()
    mavenCentral()
    mavenLocal()
}

dependencies {
    implementation "io.grpc:grpc-kotlin-stub:${grpc_kotlin_version}"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.3.61"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:${coroutines_version}"

    // gRPC and Protobuf
    implementation 'com.google.protobuf:protobuf-gradle-plugin:0.8.11'
    implementation "com.google.protobuf:protobuf-java:${protobuf_version}"
    implementation "com.google.protobuf:protobuf-java-util:3.11.1"
    implementation "io.grpc:grpc-netty-shaded:${grpc_version}"
    implementation "io.grpc:grpc-protobuf:${grpc_version}"
    implementation "io.grpc:grpc-stub:${grpc_version}"

    compileOnly "javax.annotation:javax.annotation-api:1.2"

    // Google
    implementation "com.google.guava:guava:28.2-jre"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${protobuf_version}" }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpc_version}"
        }
        grpckt {
            artifact = "io.grpc:protoc-gen-grpc-kotlin:${grpc_kotlin_version}"
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.plugins {
                // Generate Java gRPC classes
                grpc { }
                // Generate Kotlin gRPC using the custom plugin from library
                grpckt { }
            }
        }
    }
}

startScripts.enabled = false

task productInfoClient(type: CreateStartScripts) {
    mainClassName = 'ecommerce.ProductInfoClientKt'
    applicationName = 'productinfo-client'
    outputDir = startScripts.outputDir
    classpath = startScripts.classpath
}

applicationDistribution.into('bin') {
    from(productInfoClient)
    fileMode = 0755
}